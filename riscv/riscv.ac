AC_LANG_CPLUSPLUS

AC_ARG_WITH(isa,
	[AS_HELP_STRING([--with-isa=RV64IMAFDC],
		[Sets the default RISC-V ISA])],
  AC_DEFINE_UNQUOTED([DEFAULT_ISA], "$withval", [Default value for --isa switch]),
  AC_DEFINE_UNQUOTED([DEFAULT_ISA], "RV64IMAFDC", [Default value for --isa switch]))

AC_ARG_WITH(priv,
	[AS_HELP_STRING([--with-priv=MSU],
		[Sets the default RISC-V privilege modes supported])],
  AC_DEFINE_UNQUOTED([DEFAULT_PRIV], "$withval", [Default value for --priv switch]),
  AC_DEFINE_UNQUOTED([DEFAULT_PRIV], "MSU", [Default value for --priv switch]))

AC_ARG_WITH(varch,
	[AS_HELP_STRING([--with-varch=v128:e32:s128],
		[Sets the default vector config])],
  AC_DEFINE_UNQUOTED([DEFAULT_VARCH], "$withval", [Default value for --varch switch]),
  AC_DEFINE_UNQUOTED([DEFAULT_VARCH], "v128:e32:s128", [Default value for --varch switch]))


AC_SEARCH_LIBS([dlopen], [dl dld], [], [
  AC_MSG_ERROR([unable to find the dlopen() function])
])

AC_CHECK_LIB(pthread, pthread_create, [], [AC_MSG_ERROR([libpthread is required])])

AC_ARG_ENABLE([commitlog], AS_HELP_STRING([--enable-commitlog], [Enable commit log generation]))
AS_IF([test "x$enable_commitlog" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_COMMITLOG],,[Enable commit log generation])
])

AC_ARG_ENABLE([histogram], AS_HELP_STRING([--enable-histogram], [Enable PC histogram generation]))
AS_IF([test "x$enable_histogram" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_HISTOGRAM],,[Enable PC histogram generation])
])

AC_ARG_ENABLE([dirty], AS_HELP_STRING([--enable-dirty], [Enable hardware management of PTE accessed and dirty bits]))
AS_IF([test "x$enable_dirty" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_DIRTY],,[Enable hardware management of PTE accessed and dirty bits])
])

AC_ARG_ENABLE([misaligned], AS_HELP_STRING([--enable-misaligned], [Enable hardware support for misaligned loads and stores]))
AS_IF([test "x$enable_misaligned" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_MISALIGNED],,[Enable hardware support for misaligned loads and stores])
])

AC_ARG_ENABLE([rvfi_dii], AS_HELP_STRING([--enable-rvfi-dii], [Enable RISC-V Formal Interface - Direct Instruction Injection]))
AS_IF([test "x$enable_rvfi_dii" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_RVFI_DII],,[Enable RISC-V Formal Interface - Direct Instruction Injection])
])

AC_MSG_CHECKING([whether to enable CHERI])
AC_ARG_ENABLE([cheri], AS_HELP_STRING([--disable-cheri], [Disable CHERI Extension]))
AS_IF([test "x$enable_cheri" != "xno"], [
  enable_cheri=yes
  AC_DEFINE([ENABLE_CHERI],,[Enable CHERI Extension])
])
AC_MSG_RESULT([$enable_cheri])

dnl Enable merged register file by default if CHERI is enabled
AC_MSG_CHECKING([whether to enable CHERI merged register file])
AC_ARG_ENABLE([mergedrf],
  AS_HELP_STRING([--disable-mergedrf], [Disable Merged Register File]),
  [], [enable_mergedrf=$enable_cheri])
AS_IF([test "x$enable_mergedrf" != "xno"], [
  AS_IF([ test "x$enable_cheri" = "xno" ],
    AC_MSG_ERROR([Cannot enable merged register file without CHERI])
  )
  enable_mergedrf=yes
  AC_DEFINE([CHERI_MERGED_RF],,[Enable CHERI Merged RF])
])
AC_MSG_RESULT([$enable_mergedrf])

AC_ARG_ENABLE([hpm], AS_HELP_STRING([--enable-hpm], [Enable Hardware Performance Monitor]))
AS_IF([test "x$enable_hpm" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_HPM],,[Enable hardware performance monitoring in xhpm[event|counter] registers])
])
